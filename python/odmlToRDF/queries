#-- Use this query + dependent queries to explain how using OWL can ease exploring the
#-- data structure as well as the content of the graph.

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix odml: <https://g-node.org/odml-rdf#>

SELECT ?type
WHERE {
  ?subject rdf:type ?type .
}
GROUP by ?type ORDER BY ?type


#-- alternative keywords query using odml subclass

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix odml: <https://g-node.org/odml-rdf#>

SELECT ?available_keywords (COUNT(?available_keywords) as ?num_keywords)
WHERE {
  ?sec rdf:type odml:Subject .
  ?sec odml:hasProperty ?property .
  ?property odml:hasValue ?value .
  ?value rdfs:member ?available_keywords .
}
GROUP BY ?available_keywords ORDER BY DESC( ?num_keywords )


#-- Query using odml:Identifier subclass to simplify keyword query

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix odml: <https://g-node.org/odml-rdf#>

SELECT ?file ?keyword ?id_type_value ?doi_link
WHERE {
  ?doc rdf:type odml:Document .
  ?doc odml:hasFileName ?file .
  ?doc odml:hasSection ?s .
  ?s odml:hasSection ?ids .
  ?ids rdf:type odml:Identifier .
  ?ids odml:hasProperty ?idp .
  ?idp odml:hasName "identifier" .
  ?idp odml:hasValue ?doival .
  ?ids odml:hasProperty ?pt .
  ?pt odml:hasName "identifierType" .
  ?pt odml:hasValue ?idtype .
  ?idtype rdfs:member ?id_type_value .
  ?doival rdfs:member ?doi_val .
  ?s odml:hasSection ?subcont .
  ?s odml:hasName ?sec_name .
  ?subcont odml:hasSection ?subj .
  ?subj odml:hasProperty ?p .
  ?p odml:hasName ?prop_name .
  ?p odml:hasValue ?v .
  {?v rdfs:member "Neuroscience"} UNION {?v rdfs:member "Electrophysiology"} .
  ?v rdfs:member ?keyword .
  BIND(CONCAT("https://doi.org/", ?doi_val) AS ?doi_link)
}
ORDER BY ?file
LIMIT 100


#-- query with DOI as proper URI

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix odml: <https://g-node.org/odml-rdf#>

SELECT ?subject ?prop ?name ?doi ?doi_link
WHERE {
  ?subject rdf:type odml:Identifier .
  ?subject odml:hasProperty ?prop .
  ?prop odml:hasName "identifier" .
  ?prop odml:hasValue ?val .
  ?val rdfs:member ?doi .
  BIND(URI(CONCAT("https://doi.org/", ?doi)) AS ?doi_link)
}
ORDER BY ?subject
LIMIT 100


#-- Author information query

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix odml: <https://g-node.org/odml-rdf#>

SELECT ?sec ?pnval ?n ?subval
WHERE {
  ?sec rdf:type odml:Creator .
  ?sec odml:hasProperty ?p .
  ?p odml:hasName ?pn .
  ?p odml:hasValue ?pnv .
  ?pnv rdfs:member ?pnval .
  ?sec odml:hasSection ?subsec .
  ?subsec odml:hasProperty ?prop .
  ?prop odml:hasName ?n .
  ?prop odml:hasValue ?subv .
  ?subv rdfs:member ?subval .
}
order by ?sec ?n
LIMIT 100


#-- find section via value in property

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix odml: <https://g-node.org/odml-rdf#>

SELECT ?sec ?sec_name ?prop_name ?val_str
WHERE {
  ?sec odml:hasName ?sec_name .
  ?sec odml:hasProperty ?prop .
  ?prop odml:hasName ?prop_name .
  ?prop odml:hasValue ?val .
  ?val rdfs:member ?val_str .
  FILTER(STRSTARTS(?val_str, "Ludwig"))
}
ORDER BY ?sec_name ?prop_name ?val_str
LIMIT 100


#-- find all sections via property name

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix odml: <https://g-node.org/odml-rdf#>

SELECT ?sec ?sec_name (COUNT(DISTINCT ?cs) AS ?child_sec) (COUNT(DISTINCT ?cp) AS ?child_prop)
WHERE {
  ?sec odml:hasName ?sec_name .
  OPTIONAL {?sec odml:hasSection ?cs .}
  ?sec odml:hasProperty ?cp .
  {
    SELECT ?sec WHERE {
      ?sec odml:hasProperty ?prop .
      ?prop odml:hasName "identifier" .
    }
  }
}
GROUP BY ?sec ?sec_name ORDER BY DESC(?child_prop) ?sec_name
LIMIT 100


